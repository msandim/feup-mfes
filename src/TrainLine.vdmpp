class TrainLine
types
	public String = seq of char; 
values

instance variables
	private modules: map String to Module := {|->};
	private trains: map String to Train := {|->};
	
	private headModuleId: [String] := nil;
	private tailModuleId: [String] := nil;
	private isLineBuilt: bool := false;
	
	-- headModuleId and tailModuleId must be both null before any modules being added
	inv (headModuleId = nil and tailModuleId = nil) => card dom modules = 0;
	inv isLineBuilt = true => (headModuleId <> nil and tailModuleId <> nil);
	
	-- There arent any trains in the same block
	inv isLineBuilt => not exists t1, t2 in set dom trains & (t1 <> t2 and trains(t1).getCurrentBlock() = trains(t2).getCurrentBlock());
	
	-- There must be a minimum of 2 stations
	inv isLineBuilt => exists s1, s2 in set dom modules & (s1 <> s2 and modules(s1).getIsStation() and modules(s2).getIsStation());
	
	-- There must be at least 1 module between stations and at least 2 stations
	inv isLineBuilt => forall m in set dom modules &
		(modules(m).getIsStation() =>
			modules(m).getNextBlock(<Up>).isInStation() = false
			and modules(m).getNextBlock(<Down>).isInStation() = false) 
	
operations
	
	-- ** Adds a simple module (between 2 stations) to the line: **
	public addSimpleModule: (String) ==> ()
	addSimpleModule(id) ==
	(
		dcl simpleModule: Module := new SimpleModule(id, self);
		addModule(id, simpleModule);
	)
	pre isLineBuilt = false;
	
	-- Adds a station module to the line:
	public addStationModule: (String) ==> ()
	addStationModule(id) ==
	(
		dcl stationModule: Module := new StationModule(id, self);
		addModule(id, stationModule);
	)
	pre isLineBuilt = false;
	
	-- Closes the circular track:
  public closeCircularTrack: () ==> ()
	closeCircularTrack() ==
	(
		-- Close the circular track:
		modules(headModuleId).setDownModule(modules(tailModuleId));
		modules(tailModuleId).setUpModule(modules(headModuleId));
		
		-- The semaphores of simple modules in the station must be red or yellow
		for all id in set dom modules do
		(
			dcl module: Module := modules(id);
			
			if module.getIsStation() then
			(
				-- In the station all sempahores are yellow:
				module.getBlock(<Down>).setSemaphore(<Yellow>, <Down>);
				module.getBlock(<Up>).setSemaphore(<Yellow>, <Up>);
				
				-- If we leave the station, we have to face a red semaphore:
				module.getDownModule().getBlock(<Down>).setSemaphore(<Red>, <Down>);
				module.getUpModule().getBlock(<Up>).setSemaphore(<Red>, <Up>);
			);
			
		);
		
		-- The line is now built:
		isLineBuilt := true;
	)
	pre isLineBuilt = false and card dom modules >= 2 -- To close a circular track we have to have at least two modules in the track
	post modules(headModuleId).getDownModule() = modules(tailModuleId) and -- the first module must be linked to the last model
		modules(tailModuleId).getUpModule() = modules(headModuleId); -- The last module must be linked to the first model
	
	-- Adds a train to the train line:
	public addTrain: (String) * (Train`Orientation) * (String) ==> ()
	addTrain(id, orientation, moduleId) ==
	(
		trains := trains ++ {id |-> new Train(id, orientation, modules(moduleId).getBlock(orientation))};
		modules(moduleId).getBlock(orientation).getPreviousBlock(orientation).trainExit(orientation);
		modules(moduleId).getBlock(orientation).trainEnter(orientation);
	)
	pre isLineBuilt = true and
	id not in set dom trains and 
	moduleId in set dom modules and
	modules(moduleId).getIsStation() and -- We can only put new trains in stations
	not exists t1 in set dom trains & trains(t1).getCurrentBlock().getModule() = modules(moduleId) -- The station must be empty
	post id in set dom trains and
		trains(id).getCurrentBlock().getSemaphore(orientation).getColor() = <Red> and trains(id).getCurrentBlock().getSemaphore(orientation).getSensor() = <Busy> and -- The block is occupied and busy
		trains(id).getCurrentBlock().getModule() = modules(moduleId);
	
	-- Moves a train with a specific id
	public moveTrain: (String) ==> ()
	moveTrain(id) == trains(id).move()
	pre isLineBuilt = true and id in set dom trains;
	
	-- Stop train in a simple block
	public stopTrain: (String) ==> ()
	stopTrain(id) == trains(id).stopAction()
	pre isLineBuilt = true and id in set dom trains;
	
	-- Requests a specific train to leave a station
	public requestLeaveStation: (String) ==> bool
	requestLeaveStation(id) == trains(id).requestLeaveStation()
	pre isLineBuilt = true and id in set dom trains;
	
	public changeSemaphoreAvailability: String * Train`Orientation * bool ==> ()
	changeSemaphoreAvailability(moduleId, orientation, isAvailable) == modules(moduleId).getBlock(orientation).getSemaphore(orientation).setAvailability(isAvailable)
	pre moduleId in set dom modules;
	
	public changeSensorAvailability: String * Train`Orientation * bool ==> ()
	changeSensorAvailability(moduleId, orientation, isAvailable) == modules(moduleId).getBlock(orientation).getSemaphore(orientation).setSensorAvailability(isAvailable)
	pre moduleId in set dom modules;
	
	-- *****************************************************************
	-- ****************** Private Auxiliar Methods *********************
	-- *****************************************************************
	
	-- Adds a module to the station
	private addModule: (String) * (Module) ==> ()
	addModule(newModuleId, newModule) ==
	(
		if card dom modules > 0 -- if we already have a module added
		then
		(
			dcl tailModule: Module := modules(tailModuleId);			
			
			-- Connect the new model to the last model insered:
			tailModule.setUpModule(newModule);
			newModule.setDownModule(tailModule);
			
			-- Update the last model inserted:
			tailModuleId := newModuleId;
		)
		else
		(
			atomic
			(
				headModuleId := newModuleId;
				tailModuleId := newModuleId;
			);
		);
		
		modules := modules munion { newModuleId |-> newModule };
	)
	pre isLineBuilt = false and newModuleId not in set dom modules
	post newModuleId in set dom modules and -- The id was added to the modules
	  if card dom modules~ = 0
	  	then (headModuleId = newModuleId and tailModuleId = newModuleId)
	    else (tailModuleId = newModuleId and modules(tailModuleId~).getUpModule() = newModule and newModule.getDownModule() = modules(tailModuleId~));
		
	public pure getTrain: String ==> Train
	getTrain(id) == return trains(id);
	
	public pure getModule: String ==> Module
	getModule(id) == return modules(id);
	
	public pure getBlockSemaphore: String * Train`Orientation ==> Semaphore`Color
	getBlockSemaphore(idArg, orientationArg) ==
	(
		getModule(idArg).getBlock(orientationArg).getSemaphore(orientationArg).getColor();
	);

functions

traces

end TrainLine