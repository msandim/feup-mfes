class Block

types
	public Semaphore = <Red> | <Yellow> | <Green> | <Unavailable>;
	public Sensor = <Free> | <Busy> | <Unavailable>;
		
instance variables
	private module : Module;
	private trainLine: TrainLine;
	
	private sensor: Sensor := <Free>;
	private upSemaphore: Semaphore := <Green>;
	private downSemaphore: Semaphore := <Green>;
	private true_self: Block;
	
	-- Invariants:
	--inv (sensor = <Free> and upSemaphore <> <Red> and downSemaphore <> <Red>) or (sensor = <Busy> and 
	
	-- Sensor must be unavailable or mark if a train is in the block
	/** inv (sensor = <Free> and
			not exists t in set rng trainLine.getTrains() & t.getCurrentBlock() = true_self)
		or (sensor = <Busy> and
			exists1 t in set rng trainLine.getTrains() & t.getCurrentBlock() = true_self)
		or sensor = <Unavailable>; */
	
	-- Semaphore must be unavailable or mark if a train is in the block
	/*inv ((semaphore = <Green> or semaphore = <Yellow>) and
			not exists t in set (rng trainLine.getTrains()) & t.getCurrentBlock() = true_self)
		or (semaphore = <Red> and
			exists1 t in set (rng trainLine.getTrains()) & t.getCurrentBlock() = true_self)
		or semaphore = <Unavailable>;*/
	
operations

	-- Constructor
	public Block: (Module) * (TrainLine) ==> Block
	Block(moduleArg, trainLineArg) ==
	(
		module := moduleArg;
		trainLine := trainLineArg;
		true_self := self;
		
		-- If its in a station, lets put the semaphores red
		if isInStation()
		then
		(
			upSemaphore := <Red>;
			downSemaphore := <Red>;
		);
		
		return self
	);
	
	public trainEnter: (Train`Orientation) ==> ()
	trainEnter(trainOrientation) ==
	(
		if trainOrientation = <Up>
		then atomic
		(
			upSemaphore := <Red>;
			sensor := <Busy>;
		)
		else atomic
		(
			downSemaphore := <Red>;
			sensor := <Busy>;
		);	
	);
	
	public trainExit: (Train`Orientation) ==> ()
	trainExit(trainOrientation) ==
	(
		dcl previousBlock: Block := getPreviousBlock(trainOrientation);
	
		if trainOrientation = <Up>
		then atomic
		(
			upSemaphore := <Yellow>;
			sensor := <Free>;
		)
		else atomic
		(
			downSemaphore := <Yellow>;
			sensor := <Free>;
		);
		
		-- TODO falta meter para nos coisos antes das estações n ser verde
		previousBlock.setSemaphore(<Green>, trainOrientation);
	);
	
	-- Methods
	pure public canEnter: (Train`Orientation) ==> (bool)
	canEnter(orientationArg) ==
	(
		if orientationArg = <Up>
		then
			return upSemaphore <> <Red>
		else
			return downSemaphore <> <Red>;
	);
	
	pure public canEnterNextBlock: (Train`Orientation) ==> (bool)
	canEnterNextBlock(orientation) ==
	(
		return getNextBlock(orientation).canEnter(orientation);
	);
	
	pure public isInStation: () ==> (bool)
	isInStation() ==
	(
		return module.getIsStation();
	);
	
	-- Gets
	pure public getSemaphore: (Train`Orientation) ==> (Semaphore)
	getSemaphore(orientationArg) == 
	(
		if orientationArg = <Up>
		then
			return upSemaphore
		else
			return downSemaphore;
	);
	
	pure public getSensor: () ==> (Sensor)
	getSensor() == return sensor;
	
	pure public getModule: () ==> (Module)
	getModule() ==
	(
		return module;
	);
	
	pure public getNextBlock: (Train`Orientation) ==> (Block)
	getNextBlock(orientation) == 
	(
		return module.getNextBlock(orientation);
	);
	
	pure public getPreviousBlock: (Train`Orientation) ==> (Block)
	getPreviousBlock(orientation) == 
	(
		return module.getPreviousBlock(orientation);
	);
	
	-- Sets
	public setTrainLine: (TrainLine) ==> ()
	setTrainLine(initialTrainLine) ==
	(
		trainLine := initialTrainLine;
	);
	
	public setSemaphore: (Semaphore) * (Train`Orientation) ==> ()
	setSemaphore(semaphoreArg, orientationArg) ==
	(	
		if orientationArg = <Up>
		then
			upSemaphore := semaphoreArg
		else
			downSemaphore := semaphoreArg;
	);

end Block