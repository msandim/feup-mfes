class Block

types
		
instance variables
	private module : Module;
	private trainLine: TrainLine;
	
	private upSemaphore: Semaphore := new Semaphore();
	private downSemaphore: Semaphore := new Semaphore();
	private true_self: Block;
	
	-- Invariants:
	--inv (sensor = <Free> and upSemaphore <> <Red> and downSemaphore <> <Red>) or (sensor = <Busy> and 
	
	-- Sensor must be unavailable or mark if a train is in the block
	/** inv (sensor = <Free> and
			not exists t in set rng trainLine.getTrains() & t.getCurrentBlock() = true_self)
		or (sensor = <Busy> and
			exists1 t in set rng trainLine.getTrains() & t.getCurrentBlock() = true_self)
		or sensor = <Unavailable>; */
	
	-- Semaphore must be unavailable or mark if a train is in the block
	/*inv ((semaphore = <Green> or semaphore = <Yellow>) and
			not exists t in set (rng trainLine.getTrains()) & t.getCurrentBlock() = true_self)
		or (semaphore = <Red> and
			exists1 t in set (rng trainLine.getTrains()) & t.getCurrentBlock() = true_self)
		or semaphore = <Unavailable>;*/
	
operations

	-- Constructor
	public Block: (Module) * (TrainLine) ==> Block
	Block(moduleArg, trainLineArg) ==
	(
		module := moduleArg;
		trainLine := trainLineArg;
		true_self := self;
		
		return self
	);
	
	public trainEnter: (Train`Orientation) ==> ()
	trainEnter(trainOrientation) ==
	(
		if trainOrientation = <Up>
		then
		(
			upSemaphore.setColor(<Red>);
			upSemaphore.setSensor(<Busy>);
		)
		else
		(
			downSemaphore.setColor(<Red>);
			downSemaphore.setSensor(<Busy>);
		);	
	);
	
	public trainExit: (Train`Orientation) ==> ()
	trainExit(trainOrientation) ==
	(
		-- Update the block we're leaving
		dcl semaphoreNextColor: Semaphore`Color;
		dcl previousSemaphoreNextColor: Semaphore`Color;
		
		-- If this is a module after a station, it must be red at all time, otherwise yellow
		if getModule().getPreviousModule(trainOrientation).getIsStation()
		then
			semaphoreNextColor := <Red>
		else
			semaphoreNextColor := <Yellow>;
	
		if trainOrientation = <Up>
		then
		(
			upSemaphore.setColor(semaphoreNextColor);
			upSemaphore.setSensor(<Free>);
		)
		else
		(
			downSemaphore.setColor(semaphoreNextColor);
			downSemaphore.setSensor(<Free>);
		);
		
		-- Check the previous block to the block we're leaving:
		-- If the previous block from the block we're leaving is a station, leave it yellow
		if (getModule().getPreviousModule(trainOrientation).getIsStation()) then
			previousSemaphoreNextColor := <Yellow>
		elseif (getModule().getPreviousModule(trainOrientation).getPreviousModule(trainOrientation).getIsStation()) then -- If the previous-previous block is a station, then it must be red
			previousSemaphoreNextColor := <Red>
		else
			previousSemaphoreNextColor := <Green>;
			
		getModule().getPreviousModule(trainOrientation).getBlock(trainOrientation).setSemaphore(previousSemaphoreNextColor, trainOrientation);
	);
	
	-- Methods
	pure public canEnter: (Train`Orientation) ==> (bool)
	canEnter(orientationArg) ==
		if orientationArg = <Up>
		then
			return upSemaphore.getColor() <> <Red>
		else
			return downSemaphore.getColor() <> <Red>;
	
	pure public canEnterNextBlock: (Train`Orientation) ==> (bool)
	canEnterNextBlock(orientation) == return getNextBlock(orientation).canEnter(orientation);
	
	pure public isInStation: () ==> (bool)
	isInStation() == return module.getIsStation();
	
	-- Gets
	pure public getSemaphore: (Train`Orientation) ==> (Semaphore)
	getSemaphore(orientationArg) == 
		if orientationArg = <Up>
		then
			return upSemaphore
		else
			return downSemaphore;
	
	--pure public getSensor: (Train`Orientation) ==> (Sensor)
	--getSensor() == return sensor;
	
	pure public getModule: () ==> (Module)
	getModule() == return module;
	
	pure public getNextBlock: (Train`Orientation) ==> (Block)
	getNextBlock(orientation) == return module.getNextBlock(orientation);
	
	pure public getPreviousBlock: (Train`Orientation) ==> (Block)
	getPreviousBlock(orientation) == return module.getPreviousBlock(orientation);
	
	-- Sets
	public setSemaphore: (Semaphore`Color) * (Train`Orientation) ==> ()
	setSemaphore(semaphoreArg, orientationArg) ==
		if orientationArg = <Up>
		then
			upSemaphore.setColor(semaphoreArg)
		else
			downSemaphore.setColor(semaphoreArg);

end Block