class Train
types
  -- Possible orientations for a train:
	public Orientation = <Up> | <Down>;
	-- Possible status for a train:
	public MovementStatus = <Stopped> | <Moving>;
values

instance variables
	private id: TrainLine`String; -- Train identifier
	
	private orientation: Orientation := <Up>; -- Orientation of this train
	private status: MovementStatus := <Stopped>; -- Current status of this train
	private currentBlock: Block; -- Current block of the train
	
operations
	-- Constructor
	public Train: (TrainLine`String) * (Orientation) * (Block) ==> Train
	Train(idArg, initialOrientation, initialBlock) ==
	(
		id := idArg;
		orientation := initialOrientation;
		currentBlock := initialBlock;
		return self;
	);
	
	-- Moves the train to the next block
	public move: () ==> ()
	move() ==
	(
		-- Update status:
		status := <Moving>;
		
		-- Make exit operations on the block we were on:
		currentBlock.trainExit(orientation);
	
		-- Make enter operations on the block we're going on:
		getNextBlock().trainEnter(orientation);
			
		-- Move to the next block:	
		currentBlock := getNextBlock();
	)
	pre getNextBlock().canEnter(orientation)
	post currentBlock = currentBlock~.getNextBlock(orientation) and -- New block is equal to old block's next block
		not currentBlock.canEnter(orientation) and -- We cannot enter in the new block (New block is red)
	  (if getPreviousBlock().getPreviousBlock(orientation).isInStation() then -- Check if sempahore from the block we're exiting is ok
	  		getPreviousBlock().getSemaphore(orientation).getColor() = <Red>
	  	else
	  		getPreviousBlock().getSemaphore(orientation).getColor() = <Yellow>
	  ) and
	  (if getPreviousBlock().getPreviousBlock(orientation).getPreviousBlock(orientation).isInStation() 
	  			or getPreviousBlock().getPreviousBlock(orientation).getSemaphore(orientation).getSensor() = <Busy> then -- We should put red if its a block next to a station, or a train is there
	  		getPreviousBlock().getPreviousBlock(orientation).getSemaphore(orientation).getColor() = <Red>
	  	elseif getPreviousBlock().getPreviousBlock(orientation).isInStation() then
	  		getPreviousBlock().getPreviousBlock(orientation).getSemaphore(orientation).getColor() = <Yellow>
	  	else
	  		getPreviousBlock().getPreviousBlock(orientation).getSemaphore(orientation).getColor() = <Green>
	  );
	  
	-- Stops the train
	public stopAction: () ==> ()
	stopAction() == status := <Stopped>
	pre status = <Moving>;
	
	-- Requests the train to leave the current station
	public requestLeaveStation: () ==> bool
	requestLeaveStation() ==
	(
		-- See if we can exit ==> the set of blocks until the next station must be cleared and the next semaphore must work
		if isPathClearedUntilNextStation() and getNextBlock().getSemaphore(orientation).isAvailable()
		then
		(	
			getNextBlock().setSemaphore(<Green>, orientation);	
			return true;
		)
		else
			return false;
	)
	pre status = <Stopped> and currentBlock.isInStation() and getNextBlock().getSemaphore(orientation).getColor() = <Red>
	post if isPathClearedUntilNextStation() and getNextBlock().getSemaphore(orientation).isAvailable() then
					getNextBlock().getSemaphore(orientation).getColor() = <Green>
		else getNextBlock().getSemaphore(orientation).getColor() = <Red>;
	
	-- Gets the current block in which the train is
	pure public getCurrentBlock: () ==> Block
	getCurrentBlock() == return currentBlock;
	
	-- Gets the next block the train is going to occupy according to his orientation
	pure public getNextBlock: () ==> Block
	getNextBlock() == return currentBlock.getNextBlock(orientation);
	
	-- Gets the previous block the train is going to occupy according to his orientation
	pure public getPreviousBlock: () ==> Block
	getPreviousBlock() == return currentBlock.getPreviousBlock(orientation);
	
	-- Gets the train's orientation
	pure public getOrientation: () ==> Orientation
	getOrientation() == return orientation;
	
	-- Checks if a train is stopped at the moment
	pure public getIsStopped: () ==> bool
	getIsStopped() == return status = <Stopped>;
	
	-- Gets the train's identifier
	pure public getId: () ==> TrainLine`String
	getId() == return id;
	
	-- Checks if the path from a specific station to the next one is clear, as in, no sensors is occupied during the way
	private pure isPathClearedUntilNextStation: () ==> bool
	isPathClearedUntilNextStation() ==
	(
		dcl nextBlock: Block := getNextBlock();
		
		-- Iterate the next blocks and check if we can make it to the next station without being stuck somewhere in between:
		while (not nextBlock.getModule().getIsStation())
			and nextBlock.getSemaphore(orientation).getSensor() = <Free> and nextBlock.getSemaphore(orientation).isSensorAvailable()
			and nextBlock.getSemaphore(oppositeOrientation()).getSensor() = <Free> and nextBlock.getSemaphore(oppositeOrientation()).isSensorAvailable() do
				nextBlock := nextBlock.getNextBlock(orientation);
		
		-- If we got to the next station in the previous cycle, then the path is clear, but we have to check if it's busy
		-- But in the station, we only want to check if the semaphore for our orientaton is on
		return (nextBlock.getModule().getIsStation() and nextBlock.getSemaphore(orientation).getSensor() = <Free> and nextBlock.getSemaphore(orientation).isSensorAvailable());
	)
	pre status = <Stopped> and currentBlock.isInStation(); -- We can only execute this function when we're stopped in a station
	
	-- Returns the opposite orientation to the train's orientation
	public pure oppositeOrientation: () ==> (Orientation)
	oppositeOrientation() == 
	(
		if orientation = <Up>
		then return <Down>
		else return <Up>;
	);

end Train